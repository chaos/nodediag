#!/usr/bin/env perl

use strict;
use lib './lib'; # for test

use File::Basename;
use Getopt::Long qw(:config no_ignore_case bundling);
use TAP::Harness;
use TAP::Formatter::Nodediag;

my $prog = basename($0);

my $usage = <<EOF;
Usage: $prog [OPTIONS] [test [test]...]

   -f, --firstfail      Exit on first failure
   -l, --list           List diags with descriptions
   -q, --quiet          Show no output, result is exit code
   -v, --verbose        Include raw test output
   -x, --failonly       Show output only from failed tests
   -p, --plain          Don't use pretty colors in output
   -c, --config         Generate prototype sysconfig file on stdout
   -s, --sanity         Only run quick sanity tests
   -F, --forever        Repeat tests until interrupted
   -d, --testdir        Use test directory other than /etc/nodediag.d
   -h, --help           Display usage message
EOF

our %conf = ();

parse_cmdline ();

# Determine what tests to run
my @tests = get_tests ();

# Handle -l option
if ($conf{list}) {
    foreach (@tests) { system ($_, "-d"); }
    exit (0);
}
# Handle -c option
if ($conf{config}) {
    foreach (@tests) { system ($_, "-c"); }
    exit (0);
}

# execute tests using the TAP framework
my $rc;
do {
    $rc = run_tests (@tests);
} while ($conf{forever});

exit $rc;

sub get_tests
{
    my @files;

    if (@{$conf{tests}}) {
        foreach (@{$conf{tests}}) {
            log_fatal ("$_.t could not be found in $conf{testdir}\n")
                unless -r "$conf{testdir}/$_" . ".t";
            push @files, $_ . ".t";
        }
        @files = map "$conf{testdir}/$_", @files;
    } else {
        @files = glob "$conf{testdir}/*.t";
    }
    return @files;
}

sub run_tests
{
    my $formatter = TAP::Formatter::Nodediag->new (
        { color => (($conf{plain} || ! -t STDOUT) ? 0 : 1) },
    );

    if ($conf{verbose}) {         # show result of each test and raw test output
        $formatter->verbosity(1);
    } elsif ($conf{failonly}) {   # show result of failures only
        $formatter->verbosity(-1);
    } elsif ($conf{quiet}) {      # show nothing
        $formatter->verbosity(-2);
    } else {
        $formatter->verbosity(0); # show result of each test
    }
        
    my $harness = TAP::Harness->new(
        { formatter => $formatter },
        { test_args => ['-s'] },
    );

    $harness->merge(1);           # combine stdout/stderr of tests

    my @test_args = ();
    push @test_args, ('-s') if $conf{sanity};
    $harness->test_args(\@test_args);
    my $aggregator = $harness->runtests( @_ );
    return $aggregator->has_errors;
}

sub usage
{
    print STDERR "$usage";
    exit 0;
}

sub parse_cmdline
{
    my $help = 0;

    $conf{firstfail} = 0;
    $conf{list} = 0;
    $conf{quiet} = 0;
    $conf{verbose} = 0;
    $conf{failonly} = 0;
    $conf{plain} = 0;
    $conf{config} = 0;
    $conf{sanity} = 0;
    $conf{forever} = 0;
    $conf{testdir} = "/etc/nodediag.d";

    my $rc = GetOptions (
        "help|h!"         => \$help,
        "firstfail|f!"    => \$conf{firstfail},
        "list|l!"         => \$conf{list},
        "quiet|q!"        => \$conf{quiet},
        "verbose|v!"      => \$conf{verbose},
        "failonly|x!"     => \$conf{failonly},
        "plain|p!"        => \$conf{plain},
        "config|c!"       => \$conf{config},
        "sanity|s!"       => \$conf{sanity},
        "forever|F!"      => \$conf{forever},
        "testdir|d=s"     => \$conf{testdir},
    );

    log_fatal ("use only one of -v or -q\n") if $conf{quiet} && $conf{verbose};
    usage() if $help || !$rc;

    @{$conf{tests}} = @ARGV;
}

sub log_msg     { print STDERR "$prog: ", @_; }
sub log_error   { log_msg ("Error: ", @_) }
sub log_fatal   { log_msg ("Fatal: ", @_); exit 1; }

#  vi: ts=4 sw=4 expandtab
